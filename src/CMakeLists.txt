################ Various Options ################

# various macro includes
INCLUDE(FlagUtilities)
INCLUDE(InstallUtilities)
INCLUDE(SourceFileUtilities)
INCLUDE(BinaryUtilities)

############## MAVCONNConfig.h ##############

# Check endianness
INCLUDE(TestBigEndian)
TEST_BIG_ENDIAN(MAVCONN_BIG_ENDIAN)
IF(NOT MAVCONN_BIG_ENDIAN)
  SET(MAVCONN_LITTLE_ENDIAN TRUE)
ENDIF()

# 32/64 bit system check
IF(CMAKE_SIZEOF_VOID_P EQUAL 8)
  SET(MAVCONN_ARCH_64 TRUE)
ELSE()
  SET(MAVCONN_ARCH_32 TRUE)
ENDIF()

# Platforms
SET(MAVCONN_PLATFORM_WINDOWS ${WIN32})
SET(MAVCONN_PLATFORM_APPLE ${APPLE})
SET(MAVCONN_PLATFORM_UNIX ${UNIX})
IF(UNIX AND NOT APPLE)
  SET(MAVCONN_PLATFORM_LINUX TRUE)
ENDIF()

# Check __forceinline
IF(MSVC)
  INCLUDE(CheckCXXSourceCompiles)
  SET(_source "int main() { return 0; } __forceinline void test() { return; }")
  CHECK_CXX_SOURCE_COMPILES("${_source}" HAVE_FORCEINLINE)
ENDIF(MSVC)

# Check ciso646 include (literal operators)
INCLUDE(CheckIncludeFileCXX)
CHECK_INCLUDE_FILE_CXX(iso646.h HAVE_ISO646_H)

# XCode and Visual Studio support multiple configurations. In order to tell the
# which one we have to define the macros separately for each configuration
ADD_COMPILER_FLAGS("-DCMAKE_BUILD_TYPE=Debug"          Debug)
ADD_COMPILER_FLAGS("-DCMAKE_BUILD_TYPE=Release"        Release)
ADD_COMPILER_FLAGS("-DCMAKE_BUILD_TYPE=RelWithDebInfo" RelWithDebInfo)
ADD_COMPILER_FLAGS("-DCMAKE_BUILD_TYPE=MinSizeRel"     MinSizeRel)

SET(GENERATED_FILE_COMMENT
   "DO NOT EDIT THIS FILE!
    It has been automatically generated by CMake from MAVCONNConfig.h.in")
# Copy and configure MAVCONNConfig which gets included in every file
CONFIGURE_FILE(MAVCONNConfig.h.in ${CMAKE_CURRENT_BINARY_DIR}/MAVCONNConfig.h)
CONFIGURE_FILE(MAVCONNPathConfig.h.in ${CMAKE_CURRENT_BINARY_DIR}/MAVCONNPathConfig.h)

############## Include Directories ##############

# Set the search paths for include files
INCLUDE_DIRECTORIES(
  # External
  ${OPENCV_CORE_INCLUDE_DIR}

  # All library includes are prefixed with the path to avoid conflicts
  ${CMAKE_CURRENT_SOURCE_DIR}

  # MAVCONNConfig.h
  ${CMAKE_CURRENT_BINARY_DIR}
)

ADD_DEFINITIONS("-D OPENCV_VERSION_MAJOR=${OpenCV_VERSION_MAJOR} -D OPENCV_VERSION_MINOR=${OpenCV_VERSION_MINOR}")

IF (Boost_FOUND)
  INCLUDE_DIRECTORIES(${Boost_INCLUDE_DIRS})
ENDIF ()

################ Sub Directories ################

# Global includes (don't put everything here, just the most important stuff)
INCLUDE_DIRECTORIES(
  .
  core
  core/corelib     # Some common core functions
  ${MAVLINK_INCLUDE_DIR}     # MAVCONN MAVLINK library
  ${MAVLINK_TYPES_INCLUDE_DIR}     # MAVCONN MAVLINK library
  interface/shared_mem
  external
  external/lcm # MIT's LCM library http://code.google.com/p/lcm/
)

# External libraries
ADD_SUBDIRECTORY(external)

# MAVCONN code
ADD_SUBDIRECTORY(core)
ADD_SUBDIRECTORY(comm)
ADD_SUBDIRECTORY(planning)
ADD_SUBDIRECTORY(interface)
ADD_SUBDIRECTORY(calibration)
ADD_SUBDIRECTORY(examples)
# AUTO PILOT Code
ADD_SUBDIRECTORY(autopilot)

